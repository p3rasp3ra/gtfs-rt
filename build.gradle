plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.5'
}

group = 'com.marszrut'
version = '0.0.1-SNAPSHOT'
description = 'realtime animals of the marszrut.com zoo'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenCentral()
	maven { url = 'https://repo.spring.io/snapshot' }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.springframework.integration:spring-integration-mqtt'
	implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'com.google.protobuf:protobuf-java:4.28.2'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'io.micrometer:micrometer-core'
    implementation 'org.mobilitydata:gtfs-realtime-bindings:0.0.8'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.flywaydb:flyway-database-postgresql'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-flyway'
	testImplementation 'org.awaitility:awaitility:4.2.0'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

// Protobuf configuration
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.28.2'
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                java {}
            }
        }
    }
}

// Make generated sources visible to IDEs
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

// Suppress sun.misc.Unsafe warnings with Java 24 until Java 25 upgrade
tasks.withType(JavaExec).configureEach {
	jvmArgs '--sun-misc-unsafe-memory-access=allow'
}

tasks.named('bootRun') {
	jvmArgs '--sun-misc-unsafe-memory-access=allow'
}


